plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    def vouchLibs = [
        "org.bouncycastle:bcprov-jdk18on:${rootProject.bouncycastle_version}",
        "com.google.zxing:core:${rootProject.zxing_version}",
        "com.zaxxer:HikariCP:${rootProject.hikaricp_version}",
        "com.h2database:h2:${rootProject.h2_version}",
        "com.mysql:mysql-connector-j:${rootProject.mysql_version}",
        "org.postgresql:postgresql:${rootProject.postgresql_version}",
        "com.electronwill.night-config:core:${rootProject.nightconfig_version}",
        "com.electronwill.night-config:toml:${rootProject.nightconfig_version}"
    ]
    
    vouchLibs.each { lib ->
        shadowBundle lib
        implementation lib
        // Make deps visible to NeoForge's ModuleClassLoader during development
        forgeRuntimeLibrary lib
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    relocate 'org.bouncycastle', 'com.nozz.vouch.libs.bc'
    relocate 'com.google.zxing', 'com.nozz.vouch.libs.zxing'
    relocate 'com.zaxxer.hikari', 'com.nozz.vouch.libs.hikari'
    relocate 'org.h2', 'com.nozz.vouch.libs.h2'
    relocate 'com.electronwill.nightconfig', 'com.nozz.vouch.libs.nightconfig'
    relocate 'org.postgresql', 'com.nozz.vouch.libs.postgresql'
    relocate 'com.mysql', 'com.nozz.vouch.libs.mysql'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
